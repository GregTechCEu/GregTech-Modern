tasks.register('slimJar', Jar) {
    archiveClassifier = "dev-slim"
    from sourceSets.main.output
}

shadowJar {
    dependsOn shadowJar.configurations
    configurations = [project.configurations.shadow]
    archiveClassifier = "dev-shadow"
    minimize()
    relocate("org.jgrapht", "${maven_group}.repack.org.jgrapht")
    relocate("org.jheaps", "${maven_group}.repack.org.jheaps")
    relocate("org.apfloat", "${maven_group}.repack.org.apfloat")
}

obfuscation {
    reobfuscate(tasks.named('slimJar'), sourceSets.main) {
        archiveClassifier = "slim"
    }
}

jar {
    exclude ".cache/*"

    archiveClassifier = "dev"
}

tasks.register('sourcesJar', Jar) {
    from delombok.outputs.files
    dependsOn delombok
    archiveClassifier = "sources"
}

base {
    archivesName = "${project.name}-${libs.versions.minecraft.get()}"
}

afterEvaluate {
    tasks.withType(org.gradle.jvm.tasks.Jar).configureEach {
        manifest.attributes([
                'MixinConfigs': 'gtceu.mixins.json',
                'Specification-Title': project.name,
                'Specification-Version': mod_version,
                'Specification-Vendor': 'GregTechCEu Team',
                'Implementation-Title': base.archivesName,
                'Implementation-Version': version,
                'Implementation-Vendor': 'GregTechCEu Team',
        ])
    }
}
