architectury {
    common(rootProject.enabled_platforms.split(","))
}

String getConfig(String key) {
    return project.getParent().properties.get(key)
}

def mod_id = getConfig("mod_id")

loom {
    accessWidenerPath = file("src/main/resources/${mod_id}.accesswidener")
}

repositories {
    maven { url = "https://mvn.devos.one/snapshots/" } // Create Fabric, Porting Lib, Forge Tags, Milk Lib, Registrate Fabric
    //maven { url = "https://maven.cafeteria.dev/releases" } // Fake Player API
    maven { url = "https://maven.jamieswhiteshirt.com/libs-release" } // Reach Entity Attributes
    maven { url = "https://jitpack.io/" } // Mixin Extras, Fabric ASM
    maven {
        // saps.dev Maven (KubeJS and Rhino)
        url = "https://maven.saps.dev/minecraft"
        content {
            includeGroup "dev.latvian.mods"
        }
    }
    maven { // TOP
        url "https://maven.k-4u.nl"
    }
}

configurations {
//    snapshotModImplementation.extendsFrom modImplementation
//    snapshotModImplementation {
//        resolutionStrategy.cacheChangingModulesFor(0, "seconds")
//    }
    apiElements {
        outgoing {
            capability("net.fabricmc.fabric-api:fabric-api-${this.libs.versions.minecraft.get()}:${this.fabric.versions.fabricApi.get()}")
            capability("com.lowdragmc.ldlib:ldlib-common-${this.libs.versions.minecraft.get()}:${this.fabric.versions.ldlib.get()}")
            capability("com.tterrag.registrate_fabric:Registrate:${this.fabric.versions.registrate.get()}")
            capability("dev.toma.configuration:configuration-fabric-${this.libs.versions.minecraft.get()}:${this.fabric.versions.configuration.get()}")
            capability("io.github.llamalad7:mixinextras-common:${this.fabric.versions.mixinextras.get()}")
        }
    }
}

//configurations.all {
//    resolutionStrategy.cacheChangingModulesFor(0, "seconds")
//    resolutionStrategy.cacheDynamicVersionsFor(0, "seconds")
//}

dependencies {
    // We depend on fabric loader here to use the fabric @Environment annotations and get the mixin dependencies
    // Do NOT use other classes from fabric loader
    modImplementation fabric.fabricLoader
    modCompileOnly fabric.fabricApi

    // Compile against Create Fabric in common
    // beware of differences across platforms!
    // dependencies must also be pulled in to minimize problems, from remapping issues to compile errors.
    // All dependencies except Flywheel and Registrate are NOT safe to use!
    // Flywheel and Registrate must also be used carefully due to differences.
    modCompileOnly fabric.createFabric

    modCompileOnly fabric.ldlib.common
    modCompileOnly fabric.registrate

    // JEI
    modCompileOnly fabric.jei.common.api
    modCompileOnly fabric.jei.fabric.api
    modCompileOnly fabric.jei.fabric.impl

    // REI
    modCompileOnly fabric.rei.plugin
    modCompileOnly fabric.rei.api
    modCompileOnly fabric.rei.fabric

    // EMI
    modCompileOnly("dev.emi:emi-fabric:${fabric.versions.emi.get()}:api")
    modCompileOnly fabric.emi

    // TOP
    modCompileOnly(forge.theoneprobe) { transitive = false }

    // Jade
    modCompileOnly fabric.jade

    // AE2
    modImplementation(fabric.ae2) { transitive = false }

    //AlmostUnified
    modCompileOnly(fabric.almostUnified.common)

    // KJS
    modCompileOnly fabric.kubejs

    // Mixin Extras
    implementation(annotationProcessor("io.github.llamalad7:mixinextras-common:${fabric.versions.mixinextras.get()}"))

    // Configuration
    modCompileOnly fabric.configuration
}

publishing {
    publications {
        mavenCommon(MavenPublication) {
            artifactId = archivesBaseName
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        maven {
            url "https://maven.firstdarkdev.xyz/snapshots"
            credentials {
                username System.getenv("MAVEN_USER")
                password System.getenv("MAVEN_PASS")
            }
        }
    }
}
