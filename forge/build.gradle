plugins {
    alias(libs.plugins.shadow)
}

String getConfig(String key) {
    return project.getParent().properties.get(key)
}

def mod_id = getConfig("mod_id")
def common_project = project(":${mod_id}-common")
def version = getConfig("version")
def mod_license = getConfig("mod_license")
def mod_name = getConfig("mod_name")
def mod_description = getConfig("mod_description")
def mod_url = getConfig("mod_url")

architectury {
    platformSetupLoomIde()
    forge()
}

static def isJetBrainsRuntime() {
    return System.getProperty('java.vm.vendor').contains('JetBrains')
}

loom {
    accessWidenerPath = common_project.file("src/main/resources/${mod_id}.accesswidener")

    forge {
        mixinConfig(
                "${mod_id}-common.mixins.json",
                "${mod_id}.mixins.json",
        )

        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

        dataGen{
            mod(mod_id)
        }
    }

    runConfigs.named("data").configure {
        programArg("--existing=" + common_project.file("src/main/resources").absolutePath)
    }

    if (isJetBrainsRuntime()) {
        runs {
            client {
                vmArgs '-XX:+AllowEnhancedClassRedefinition'
                vmArgs '-XX:HotswapAgent=fatjar'
            }
        }
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
//    snapshotModImplementation.extendsFrom modImplementation
//    snapshotModImplementation {
//        resolutionStrategy.cacheChangingModulesFor(0, "seconds")
//    }

    apiElements {
        outgoing {
            capability("net.minecraftforge:forge:${this.forge.versions.minecratfForge.get()}")
            capability("com.lowdragmc.ldlib:ldlib-forge-${this.libs.versions.minecraft.get()}:${this.forge.versions.ldlib.get()}")
            capability("com.tterrag.registrate:Registrate:${this.forge.versions.registrate.get()}")
            capability("dev.toma.configuration:configuration-forge-${this.libs.versions.minecraft.get()}:${this.forge.versions.configuration.get()}")
            capability("io.github.llamalad7:mixinextras-forge:${this.forge.versions.mixinextras.get()}")
        }
    }
}

//configurations.all {
//    resolutionStrategy.cacheChangingModulesFor(0, "seconds")
//    resolutionStrategy.cacheDynamicVersionsFor(0, "seconds")
//}

repositories {
    // mavens for Forge-exclusives
    maven { url = "https://maven.theillusivec4.top/" } // Curios
    maven { url = "https://maven.parchmentmc.org" } // Parchment mappings
    maven { url = "https://maven.quiltmc.org/repository/release" } // Quilt Mappings
    maven { // TOP
        url "https://maven.k-4u.nl"
    }
    maven {
        // saps.dev Maven (KubeJS and Rhino)
        url = "https://maven.saps.dev/minecraft"
        content {
            includeGroup "dev.latvian.mods"
        }
    }
    maven { url 'https://jitpack.io' } // Mixin Extras, Fabric ASM
}

dependencies {
    forge forge.minecraftForge

    common(project(path: ":${mod_id}-common", configuration: "namedElements")) { transitive = false }
    shadowCommon(project(path: ":${mod_id}-common", configuration: "transformProductionForge")) { transitive = false }

    // LDLib
    modImplementation(forge.ldlib.forge) { transitive = false }
    include(forge.ldlib.forge)

    // Registrate
    modImplementation(include("com.tterrag.registrate:Registrate:${forge.versions.registrate.get()}"))

    // Create and its dependencies
    modImplementation("com.simibubi.create:create-${libs.versions.minecraft.get()}:${forge.versions.createForge.get()}:all") { transitive = false }
    modImplementation forge.flywheel

    // JEI
    modCompileOnly(forge.jei.common.api) { transitive = false }
    modCompileOnly(forge.jei.forge.api) { transitive = false }
    modCompileOnly(forge.jei.forge.impl) { transitive = false }

    // REI
    modCompileOnly forge.rei.plugin
    modCompileOnly forge.rei.api
    modImplementation forge.rei.forge

    // EMI
    modCompileOnly("dev.emi:emi-forge:${fabric.versions.emi.get()}:api")
    modCompileOnly forge.emi

    // TOP
    modCompileOnly(forge.theoneprobe) { transitive = false }

    // Jade
    modImplementation forge.jade

    // AE2
    modImplementation(forge.ae2) { transitive = false }

    //AlmostUnified
    modImplementation(forge.almostUnified.forge)

    // KJS
    modImplementation forge.kubejs

    // Mixin Extras
    implementation(annotationProcessor("io.github.llamalad7:mixinextras-common:${this.forge.versions.mixinextras.get()}"))
    implementation(include("io.github.llamalad7:mixinextras-forge:${this.forge.versions.mixinextras.get()}"))

    // Configuration
    modImplementation(forge.configuration)
    include(forge.configuration)

    // Runtime only testing mods
    modRuntimeOnly(forge.worldStripper)
}

processResources {
    // set up properties for filling into metadata
    Map<String, String> properties = Map.of(
            "version", version,
            "mod_id", mod_id,
            "forge_version", forge.versions.forgeShortVersion.get().split("\\.")[0], // only specify major version of forge
            "minecraft_version", libs.versions.minecraft.get(),
            "mod_license", mod_license,
            "mod_name", mod_name,
            "mod_description", mod_description,
            "mod_url", mod_url
    )
    properties.forEach((k, v) -> inputs.property(k, v))

    filesMatching("META-INF/mods.toml") {
        expand properties
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = null
}

jar {
    archiveClassifier = "dev"
}

sourcesJar {
    def commonSources = common_project.sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = archivesBaseName
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        maven {
            url "https://maven.firstdarkdev.xyz/snapshots"
            credentials {
                username System.getenv("MAVEN_USER")
                password System.getenv("MAVEN_PASS")
            }
        }
    }
}
